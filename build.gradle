apply plugin: "application"
apply plugin: "com.liferay.source.formatter"
apply plugin: "java"

dependencies {
	compile group: "com.googlecode.java-diff-utils", name: "diffutils", version: "1.3.0"
	compile group: "com.puppycrawl.tools", name: "checkstyle", version: "8.29"
	compile group: "com.thoughtworks.qdox", name: "qdox", version: "2.0-M5"
	compile group: "commons-io", name: "commons-io", version: "2.11.0"
	compile group: "commons-logging", name: "commons-logging", version: "1.2"
	compile(group: "jaxen", name: "jaxen", version: "1.1.1") {
		exclude group: "com.ibm.icu", module: "icu4j"
	}
	compile group: "junit", name: "junit", version: "4.13.1"
	compile group: "org.antlr", name: "antlr4-runtime", version: "4.8-1"
	compile group: "org.apache.ant", name: "ant", version: "1.10.11"
	compile group: "org.apache.maven", name: "maven-artifact", version: "3.3.9"
	compile group: "org.dom4j", name: "dom4j", version: "2.1.3"
	compile group: "org.json", name: "json", version: "20220320"
	compile group: "org.reflections", name: "reflections", "version": "0.10.2"
	compile group: "xerces", name: "xercesImpl", version: "2.12.2"

	compile group: "com.liferay", name: "com.liferay.petra.function", version: "5.0.1"
	compile group: "com.liferay", name: "com.liferay.petra.lang", version: "1.0.0"
	compile group: "com.liferay", name: "com.liferay.petra.nio", version: "1.0.0"
	compile group: "com.liferay", name: "com.liferay.petra.string", version: "2.0.0"
	compile group: "com.liferay", name: "com.liferay.poshi.core", version: "1.0.85"
	compile group: "org.codehaus.groovy", name: "groovy", version: "2.4.21"

	compile group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.1.0"
	compile group: "com.liferay", name: "com.liferay.petra.concurrent", version: "1.1.0"
	compile group: "com.liferay", name: "com.liferay.petra.memory", version: "1.0.0"
	compile group: "com.liferay", name: "com.liferay.petra.reflect", version: "1.1.0"
	compile group: "com.liferay", name: "com.liferay.petra.xml", version: "1.0.0"
	compile group: "com.liferay", name: "com.liferay.portal.tools.java.parser", version: "1.0.47"
	compile group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "latest.release"
	compile group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "latest.release"
	compile group: "com.liferay.portal", name: "com.liferay.util.java", version: "2.5.0"
	compile group: "org.apache.felix", name: "org.apache.felix.http.servlet-api", version: "1.1.2"
	compile group: "org.apache.maven", name: "maven-plugin-api", transitive: false, version: "3.0.4"

	compile group: "com.github.fracpete", name: "rsync4j-all", version: "3.2.7-1"
}

repositories {
	maven {
		url "https://repository.liferay.com/nexus/content/groups/public"
	}

	maven {
		url "https://repository-cdn.liferay.com/nexus/content/groups/public"
	}

	mavenCentral()
}

application {
	mainClassName = "com.liferay.source.formatter.SourceFormatter"
}

task cleanCode
task pullCode
task pushCode

cleanCode {
	group "dev"
	description "Cleans up all Source Formatter code"
	doLast {
		delete files("./src/main/java/com/liferay/source", "./src/main/resources")
	}
}

pullCode {
	group "dev"
	dependsOn("cleanCode")
	description "Pulls code from liferay-portal"
	doLast {
		def repoDir = project.findProperty('liferay.portal.repo.dir')
		if (repoDir == null || repoDir.trim().length() == 0) {
			throw new IllegalArgumentException("please supply path to 'liferay.portal.repo.dir' in 'gradle.properties'")
		}

		javaexec {
			classpath = sourceSets.main.runtimeClasspath
			main = "com.liferay.dev.script.RSyncCode"
			args = ["pull", repoDir]
		}
	}
}

pushCode {
	group "dev"
	description "Pushes changes to liferay-portal"
	doLast {
		def repoDir = project.findProperty('liferay.portal.repo.dir')
		if (repoDir == null || repoDir.trim().length() == 0) {
			throw new IllegalArgumentException("please supply path to 'liferay.portal.repo.dir' in 'gradle.properties'")
		}

		javaexec {
			classpath = sourceSets.main.runtimeClasspath
			main = "com.liferay.dev.script.RSyncCode"
			args = ["push", repoDir]
		}
	}
}